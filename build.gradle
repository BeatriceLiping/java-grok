plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'

// Apply code quality checks
apply from: "$rootProject.projectDir/gradle/codeQuality.gradle"
// Publish artifact to Maven repositiory
if (project.hasProperty("signing")) {
  apply from: "$rootProject.projectDir/gradle/publishArtifact.gradle"
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.1'
}

repositories {
  mavenLocal()
  mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_7
group = "de.wellnerbou"
archivesBaseName = "grok"
version = '0.1.5'

dependencies {
  compile "com.github.tony19:named-regexp:0.2.4"
  compile "org.apache.commons:commons-lang3:3.4"
  compile "com.google.code.gson:gson:2.7"
  compile "org.slf4j:slf4j-api:1.7.21"

  testCompile group: 'junit', name: 'junit', version: '4.12'
}


// user and key configuration for bintray deployment:
// ./gradlew bintrayUpload -PpublishUser=<USER> -PpublishKey=<KEY>
if (!project.hasProperty("publishUser")) {
    ext.publishUser = ""
}
if (!project.hasProperty("publishKey")) {
    ext.publishKey = ""
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'java-grok'
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = "$publishUser"
    key = "$publishKey"
    publications = ['mavenJava']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = 'java-grok'
        licenses = ['Apache 2.0']
        vcsUrl = 'https://gitlab.com/paulwellnerbou/java-grok.git'
    }
}
